# Proposal: Generate Ballerina client code using `bal persist generate` command

_Owners_: @daneshk @MadhukaHarith92 @sahanHe  
_Reviewers_: @daneshk  
_Created_: 2022/08/09   
_Updated_: 2022/08/09  
_Issues_: [#3236](https://github.com/ballerina-platform/ballerina-standard-library/issues/3236)

## Summary
We need to support Ballerina Persistent Layer on top of Ballerina SQL modules and support DB operations easily without writing any SQL statements. As a part of it, the CLI tool will be provided to support several operations. One of those operations is to generate Ballerina client code for the entity records defined in the Ballerina project. In this proposal, we describe how this requirement can be facilitated.

## Goals
To support generating Ballerina client code.

## Motivation
To generate Ballerina client codes corresponding to the entity records defined by the users. Users can then use these client objects to perform database operations programmatically without having to write SQL statements.

## Description
Consider the following Ballerina project named `medical-center`.
```
medical-center
├── Ballerina.toml
├── entities.bal
└── main.bal
```

Inside the Ballerina project, users can define `Entity` records as follows.

```ballerina
import ballerina/time;
import ballerina/persist;

@persist:Entity {
    key: ["needId"],
    tableName: "MedicalNeeds"
}
public type MedicalNeed record {|
    @persist:AutoIncrement
    readonly int needId = -1;

    int itemId;
    int beneficiaryId;
    time:Civil period;
    string urgency;
    int quantity;
|};
```

In this example, `MedicalNeed` is an entity with the attributes `itemId`, `beneficiaryId`, `period`, `urgency`, and `quantity`. When users execute `bal persist generate` inside a Ballerina project, a new module named `clients` will be created. Inside the `clients` module, Ballerina files will be generated encapsulating the client object with respect to each entity defined in the project. The following is the project structure after the command execution.
```
medical-center
├── Ballerina.toml
├── entities.bal
├── main.bal
└── modules
    └── clients
        └── medicalneed_client.bal
```

The following client object is generated inside the `medicalneed_client.bal`.

```ballerina
import ballerina/sql;
import ballerinax/mysql;
import ballerina/time;
import ballerina/persist;

client class MedicalNeedClient {

    private final string entityName = "MedicalNeed";
    private final sql:ParameterizedQuery tableName = `MedicalNeeds`;

    private final map<persist:FieldMetadata> fieldMetadata = {
        needId: {columnName: "needId", 'type: int, autoGenerated: true},
        itemId: {columnName: "itemId", 'type: int},
        beneficiaryId: {columnName: "beneficiaryId", 'type: int},
        period: {columnName: "period", 'type: time:Civil},
        urgency: {columnName: "urgency", 'type: string},
        quantity: {columnName: "quantity", 'type: int}
    };
    private string[] keyFields = ["needId"];

    private persist:SQLClient persistClient;

    public function init() returns persist:Error? {
        mysql:Client dbClient = check new (host = HOST, user = USER, password = PASSWORD, database = DATABASE, port = PORT);
        self.persistClient = check new (self.entityName, self.tableName, self.fieldMetadata, self.keyFields, dbClient);
    }

    remote function create(MedicalNeed value) returns int|persist:Error? {
        sql:ExecutionResult result = check self.persistClient.runInsertQuery(value);
        return <int>result.lastInsertId;
    }

    remote function readByKey(int key) returns MedicalNeed|persist:Error {
        return (check self.persistClient.runReadByKeyQuery(key)).cloneWithType(MedicalNeed);
    }

    remote function read(map<anydata>? filter = ()) returns stream<MedicalNeed, persist:Error?>|persist:Error {
        stream<anydata, error?> result = check self.persistClient.runReadQuery(filter);
        return new stream<MedicalNeed, error?>(new MedicalNeedStream(result));
    }

    remote function update(record {} 'object, map<anydata> filter) returns persist:Error? {
        _ = check self.persistClient.runUpdateQuery('object, filter);
    }

    remote function delete(map<anydata> filter) returns persist:Error? {
        _ = check self.persistClient.runDeleteQuery(filter);
    }

    function close() returns persist:Error? {
        return self.persistClient.close();
    }

}

public class MedicalNeedStream {

    private stream<anydata, persist:Error?> anydataStream;

    public isolated function init(stream<anydata, persist:Error?> anydataStream) {
        self.anydataStream = anydataStream;
    }

    public isolated function next() returns record {|MedicalNeed value;|}|persist:Error? {
        var streamValue = self.anydataStream.next();
        if streamValue is () {
            return streamValue;
        } else if (streamValue is error) {
            return streamValue;
        } else {
            record {|MedicalNeed value;|} nextRecord = {value: check streamValue.value.cloneWithType(MedicalNeed)};
            return nextRecord;
        }
    }

    public isolated function close() returns persist:Error? {
        return self.anydataStream.close();
    }
}
```

- The `bal persist generate` command should be executed inside a valid Ballerina project. If not, an error will be thrown.
- If there are no entities defined in the Ballerina project there won't be any changes to the project.
- If there are entities defined in the Ballerina project and there is no `clients` module in the project, a new `clients` module will get added.
- If there are entities defined in the Ballerina project and there is a `clients` module in the project, the generated bal files will be updated.

## Testing
- Execute the command outside a Ballerina project. Validate if the expected error was returned.
- Execute the command inside a Ballerina project without any entity. Validate that there was no change made to the project.
- Execute the command inside a Ballerina project with an entity but without a `clients` module. Validate that a `clients` module was created and assert the content of the bal file containing the client object.
- Execute the command inside a Ballerina project with an entity and with a `clients` module. Validate that generated bal files in the `clients` module was updated accordingly. Assert the content of the bal file containing the client object.
